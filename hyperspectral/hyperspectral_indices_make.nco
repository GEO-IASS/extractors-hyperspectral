// -*-C++-*-

/* Purpose: NCO/ncap2 script to calculate hyperpectral indices from the hyperspectral reflectances 
   ( this is the input variable rfl_img(wavelenth,y,x) ) 
   by default the averages of the required reflectances is calculated. (these have the postfix suffix "_avg" )
   These averages are then used to calculate the average of the indices. 


    
   set the following flags on the ncap2 command line 
   flg_dbg - if set then print debug info (default 0) 
  
   flg_abr - if set then calculate the averages before the ratio step  (default 0)

   flg_rba - if set then calculate the ratio (pixels) before the average step  (default 1)
  
   flg_mss - set the _FillValue 1.0e36 to all values in selected reflectances where value <=0.0f (default 1)
    
   flg_bgi - calculate the Broadband-Greenness-Indices
   usage examples 
   ncap2 -v -O  -S hyperspectral_indices_make.nco   in.nc out.nc                                         ( use default flags)     
   ncap2 -v -O -s 'flg_dbg=1s;flg_rba=1s;flg_mss=1s;' -S hyperspectral_indices_make.nco   in.nc out.nc   ( set flags )


*/
   

// copy over coordinate here (so they end up at start (maybe ?)
x=x;
y=y;
wavelength=wavelength;



if(!exists(flg_dbg))
   *flg_dbg=0s;  // [flg] Print debug info

if(!exists(flg_agg))
   *flg_agg=0s;  // [flg] calculate average, minimium and maximum of each index - save them as attributes index_name@min, index_name@max, index_name@max


if(!exists(flg_abr))
   *flg_abr=0s;  // [flg] (average before ratio) - calculate averages reflectances - then calculate ratios with these avergaes
                //       result is a scalar - (stored as an attribute)  



if(!exists(flg_mss)){
  *flg_mss=1s;  // [flg] add and set _FillValue to reflectance indices
  *flt_mss=1.0e36f;
}

if(!exists(flg_std))
  *flg_std=1s;  // [flg] calculate the standard indices




if(!exists(flg_bgi))
  *flg_bgi=1s;  // [flg] calculate the Broadband Greenness Indices


if(!exists(flg_ngi))
  *flg_ngi=1s;  // [flg] calculate the Narrowband Greenness Indices

// March 2017 - this contains SWIR wavelengths 
if(!exists(flg_cni))
  *flg_cni=0s; // [flg] // Canopy Nitrogen Indices

// March 2017 - this contains SWIR wavelengths - so default is OFF
if(!exists(flg_sci)) 
  *flg_sci=0s; // [flg] // Dry or Senescent Carbon Indices 

if(!exists(flg_lpi))
  *flg_lpi=1s; // [flg] // Leaf Pigments Indices

// March 2017 - this contains SWIR wavelengths -so default is OFF
if(!exists(flg_cwci))
  *flg_cwci=0s; // [flg] // Canopy Water Content Indices

if(!exists(flg_sdi))
  *flg_sdi=1s; // [flg] // Spectral Disease Indices

// March 2017 - this contains SWIR wavelengths -so default is OFF
if(!exists(flg_dwsi))
  *flg_dwsi=0s; // [flg] Disease Water Stress Indices

// March 2017 - this contains  LSI that is commented out as it contains SWIR elements
if(!exists(flg_mi))
  *flg_mi=1s; // [flg] // Misc Indices + Stress Detection Indices




//required reflectances
@rfl_lst={"R415"s,"R420"s,"R430"s,"R435"s,"R440"s,"R445"s,"R450"s,"R470"s, "R500"s,"R510"s,"R512"s,"R513"s,"R520"s,"R531"s,"R534"s, "R540"s,"R550"s,"R554"s,"R570"s,"R584"s,"R586"s,"R590"s,"R600"s,"R650"s,"R670"s,"R677"s, "R680"s,"R690"s,"R695"s,"R698"s, "R700"s,"R704"s,"R705"s,"R710"s,"R715"s,"R726"s,"R720"s,"R724"s,"R726"s,"R734"s,"R740"s,"R747"s,"R750"s,"R760"s, "R780"s,"R790"s,"R800"s,"R900"s,"R970"s};  

// nb ONE-TO_ONE  correspondence with above list
@dbl_val_lst={4.15e-07, 4.2e-07, 4.3e-07, 4.35e-07, 4.4e-07, 4.45e-07, 4.5e-07, 4.7e-07, 5e-07, 5.1e-07, 5.12e-07, 5.13e-07, 5.2e-07, 5.31e-07, 5.34e-07, 5.4e-07, 5.5e-07, 5.54e-07, 5.7e-07, 5.84e-07, 5.86e-07, 5.9e-07, 6e-07, 6.5e-07, 6.7e-07, 6.77e-07, 6.8e-07, 6.9e-07, 6.95e-07, 6.98e-07, 7e-07, 7.04e-07, 7.05e-07, 7.1e-07, 7.15e-07, 7.26e-07, 7.2e-07, 7.24e-07, 7.26e-07, 7.34e-07, 7.4e-07, 7.47e-07, 7.5e-07, 7.6e-07, 7.8e-07, 7.9e-07, 8e-07, 9e-07, 9.7e-07};


// use vpointers to calculate averages AFTER pixel ratios 
// 6-March 2017 - list not used right now but could be useful later 
//@ind_lst={"NDVI"s,"SR"s, "OSAVI"s,"CHL"s,"msR705"s,"TCARI"s,"CarChap"s,"Car1Black"s,"Car2Black"s,"PRI570"s,"SIPI"s,"antGamon"s,"antGitelson"s,"CHLDela"s,"CI"s,"PRI586"s,"PRI512"s,"FRI1"s,"FRI2"s,"NDVI1"s,"RERI"s,"ZM"s,"REP"s,"NDRE"s,"TVI"s};



/* sanity check */
if( @rfl_lst.size() != @dbl_val_lst.size() )
{
  print("size difference between list @rfl_lst and @dbl_val_lst\n");
  exit(1);
}



/* extract the required reflectances - 
   nb min_coords() -extracts the index of the grid value nearest the required value 
   This step creates the vars iR445, iR450, iR470..iR970 */
{
  *idx=0;
  *sz=@rfl_lst.size();

  print(@rfl_lst.size());
   
  for(idx=0;idx<sz;idx++)
  {
    @R_nm=sprint(@rfl_lst(idx));
    @iR_nm=push("i",@R_nm);
    // @R_nbr=atoi(@R_nm(1:)).double()*1.0e-9d;
    //*@iR_nm=min_coords(wavelength,@R_nbr);
    
    //print(@R_nbr);

    // nb '**' create a RAM var from a var-pointer
    **@iR_nm=min_coords(wavelength,@dbl_val_lst(idx));
       
}


}

/* sanity checks for required relectances -  make sure the are within reasonable tolerance (1.0e-9) */
if( fabs(wavelength(iR445)-4.45e-7) > 0.01e-7d )  
{
  print("wavelength R445 not in wavelength coord\n");  
  exit(1);
}
if( fabs(wavelength(iR970)-9.70e-7) > 0.01e-7d )
{
  print("wavelength R970 not in wavelength coord\n");  
  exit(1);
}
if( fabs(wavelength(iR700)-7e-7) > 0.01e-7d )
{
  print("wavelength R700 not in wavelength coord\n");  
  exit(1);
}



/* This block hyperslabs the reflectances and their averages - with or with _FillValue */
/* That is the vars R445, R450..R970  and  R445_avg, R450_avg..R970_avg */
{
 

  if(flg_dbg)
    print(@rfl_lst);

  sz=@rfl_lst.size();

  for(idx=0; idx<sz ; idx++)
  {
    @var_nm=sprint(@rfl_lst(idx));
    @var_inm=push("i",@var_nm);   
    @var_nm_avg=push(@var_nm,"_avg");

    /* create R* variables from hyperslab */
    /* nb '**' means screate a RAM var from a var-pointer */
    **@var_nm=rfl_img(*@var_inm,:,:);
   

    
    /* set and add missing value */   
   if(flg_mss) 
   {
    where (*@var_nm <= 0.0f)
      *@var_nm=flt_mss;  

   }

   /* add SoilRemovalMask */
   if(flg_mss && exists(SoilRemovalMask))
   {
     where( SoilRemovalMask != 0)      
       *@var_nm=flt_mss;

   } 
   
   if(flg_mss) 
      *@var_nm.set_miss(flt_mss);  
  


    /* create avg of hyperslabs */
   if(flg_abr)
    *@var_nm_avg=*@var_nm.avg();
  
    if(flg_dbg)
       print(@var_nm,"%s\n");
 
  } 

}

if(flg_std){

  ND900_680 = ( R900 -R680) / (R900+R680 );
  push(&@ind_lst, "ND900_680"s);
  ND900_680@long_name="Normalized Difference Vegetation Index";
  ND900_680@standard_name="normalized_difference_vegetation_index";
  ND900_680@description="Normalized Difference Vegetation Index | (R900-R680)/(R900+R680) | Rouse et al. (1973)";
  ND900_680@notes="Normalized Difference Vegetation Index | ND900_680";
  ND900_680@units="ratio";
  NDV@label="Reflectance Index";
  ND900_680@type="trait";
  if(flg_abr)
    ND900_680@abr = ( R900_avg -R680_avg) / (R900_avg+R680_avg );


  SR900_680=R900 / R680;
  push(&@ind_lst, "SR900_680"s);
  SR900_680@long_name="Simple ratio";
  SR900_680@standard_name="simple_ratio";
  SR900_680@description="Simple ratio | R900 / R680 | Rouse et al. (1973)";
  SR900_680@notes="Simple ratio | SR900_680";
  SR900_680@units="ratio";
  SR900_680@label="Reflectance Index";
  SR900_680@type="trait";
  if(flg_abr)
    SR900_680@abr=R900_avg / R680_avg;


  OSAVI=1.16f*( (R800-R670) ) /  ( R800+R670+0.16f);
  push(&@ind_lst, "OSAVI"s);
  OSAVI@long_name="Optimized Soil-Adjusted Vegetation index";
  OSAVI@standard_name="optimized_soil-adjusted_vegetation_index";
  OSAVI@description="Optimized Soil-Adjusted Vegetation index | 1.16f*( (R800-R670) ) /  ( R800+R670+0.16f) | Rondeaux et al. (1996)";
  OSAVI@notes="Optimized Soil-Adjusted Vegetation index | OSAVI";
  OSAVI@units="ratio";
  OSAVI@label="Reflectance Index";
  OSAVI@type="trait";
  if(flg_abr)
    OSAVI@abr=1.16f*( (R800_avg-R670_avg) ) /  ( R800_avg+R670_avg+0.16f);
  
  


  WI=R900-R970;
  push(&@ind_lst, "WI"s);
  WI@long_name="water index";
  WI@standard_name="water_index";
  WI@description="water index | R900-R970 | Penuelas. et al. (1993)";
  WI@notes="water index | WI";
  WI@units="ratio";
  WI@label="Reflectance Index";
  WI@type="trait";
  if(flg_abr)
    WI@abr=R900_avg-R970_avg;
  

  ChlIndex=R750 / R550;
  push(&@ind_lst, "ChlIndex"s);
  ChlIndex@long_name="Chlorophyll index";
  ChlIndex@standard_name="chlorophyll_index";
  ChlIndex@description="Chlorophyll index | R750 / R550 | Gitelson and Merzlyak (1994)";
  ChlIndex@notes="Chlorophyll index | ChlIndex";
  ChlIndex@units="ratio";
  ChlIndex@label="Reflectance Index";
  ChlIndex@type="trait";
  if(flg_abr)
    ChlIndex@abr=R750_avg / R550_avg;




  msR705= (R750-R445) / (R705-R445);
  push(&@ind_lst, "msR705"s);
  msR705@long_name="Modified simple ratio 705";
  msR705@standard_name="modified_simple_ratio_705";
  msR705@description="Modified simple ratio 705 | (R750-R445) / (R705-R445) | Sims and Gamon (2002)";
  msR705@notes="Modified simple ratio 705 | msR705";
  msR705@units="ratio";
  msR705@label="Reflectance Index";
  msR705@type="trait";
  if(flg_abr)
    msR705@abr= (R750_avg-R445_avg) / (R705_avg-R445_avg);


  TCARI=3.0f*(  (R700-R670)-0.2f * (R700-R550) * (R700/R670)  );
  push(&@ind_lst, "TCARI"s);
  TCARI@long_name="Transformed chlorophyll absorption in reflectance index";
  TCARI@standard_name="transformed_chlorophyll_absorption_in_reflectance_index";
  TCARI@description="Transformed chlorophyll absorption in reflectance index | 3.0f*(  (R700-R670)-0.2f * (R700-R550) * (R700/R670)  ) | Haboudane et al. (2002)";
  TCARI@notes="Transformed chlorophyll absorption in reflectance index | TCARI";
  TCARI@units="ratio";
  TCARI@label="Reflectance Index";
  TCARI@type="trait";
  if(flg_abr)
    TCARI@abr=3.0f*(  (R700_avg-R670_avg)-0.2f * (R700_avg-R550_avg) * (R700_avg/R670_avg)  );


  TCARI_OSAVI=TCARI/OSAVI;
  push(&@ind_lst, "TCARI_OSAVI"s);
  TCARI_OSAVI@long_name="Transformed Chlorophyll Absorption in Reflectance Index/Optimized Soil-Adjusted Vegetation Index (TCARI/OSAVI )";
  if(flg_abr)
    TCARI_OSAVI@abr=TCARI@abr/OSAVI@abr;


  CarChap_=R760/R500;
  push(&@ind_lst, "CarChap"s);
  CarChap@long_name="Carotenoid index (Chappelle)";
  CarChap@standard_name="carotenoid_index_(chappelle)";
  CarChap@description="Carotenoid index (Chappelle) | R760/R500 | Chappelle et al. (1992)";
  CarChap@notes="Carotenoid index (Chappelle) | CarChap";
  CarChap@label="Reflectance Index";
  CarChap@type="trait";
  if(flg_abr)
    CarChap@abr=R760_avg/R500_avg;


  Car1Black=R800/R470;
  push(&@ind_lst, "Car1Black"s);
  Car1Black@long_name="Carotenoid index (BlackBurn)";
  Car1Black@standard_name="carotenoid_index_(blackburn)";
  Car1Black@description="Carotenoid index (BlackBurn) | R800/R470 | Blackburn (1998)";
  Car1Black@notes="Carotenoid index (BlackBurn) | Car1Black";
  Car1Black@units="ratio";
  Car1Black@label="Reflectance Index";
  Car1Black@type="trait";
  if(flg_abr)
    Car1Black@abr=R800_avg/R470_avg;
  


  Car2Black = ( R800 - R470 ) / (R800 + R470);
  push(&@ind_lst, "Car2Black"s);
  Car2Black@long_name="Carotenoid index 2 (BlackBurn)";
  Car2Black@standard_name="carotenoid_index_2_(blackBurn)";
  Car2Black@description="Carotenoid index 2 (BlackBurn) | ( R800 - R470 ) / (R800 + R470) | Blackburn (1998)";
  Car2Black@notes="Carotenoid index 2 (BlackBurn) | Car2Black";
  Car2Black@units="ratio";
  Car2Black@label="Reflectance Index";
  Car2Black@type="trait";
  if(flg_abr)
    Car2Black@abr= ( R800_avg - R470_avg ) / (R800_avg + R470_avg);

  


  PRI570 = (R531 - R570) / (R531+R570);
  push(&@ind_lst, "PRI570"s);
  PRI570@long_name="Photochemical reflectance index (570)";
  PRI570@standard_name="photochemical_reflectance_index_(570)";
  PRI570@description="Photochemical reflectance index (570) | (R531 - R570) / (R531+R570) | Gamon et al. (1992)";
  PRI570@notes="Photochemical reflectance index (570) | PRI570";
  PRI570@units="ratio";
  PRI570@label="Reflectance Index";
  PRI570@type="trait";
  if(flg_abr)
    PRI570@abr = (R531_avg - R570_avg) / (R531_avg+R570_avg);


  SIPI =( R800 - R450) / (R800 + R650);
  push(&@ind_lst, "SIPI"s);
  SIPI@long_name="Structure intensive pigment index";
  SIPI@standard_name="structure_intensive_pigment_index";
  SIPI@description="Structure intensive pigment index | ( R800 - R450) / (R800 + R650) | Penuelas. et al. (1995)";
  SIPI@notes="Structure intensive pigment index | SIPI";
  SIPI@units="ratio";
  SIPI@label="Reflectance Index";
  SIPI@type="trait";
  if(flg_abr)
    SIPI@abr =( R800_avg - R450_avg) / (R800_avg + R650_avg);



  AntGamon=R650/R550;
  push(&@ind_lst, "AntGamon"s);
  AntGamon@long_name="Anthocyanin (Gamon)";
  AntGamon@standard_name="anthocyanin_(gamon)";
  AntGamon@description="Anthocyanin (Gamon) | R650/R550 | Gamon and Surfus (1999)";
  AntGamon@notes="Anthocyanin (Gamon) | AntGamon";
  AntGamon@units="ratio";
  AntGamon@label="Reflectance Index";
  AntGamon@type="trait";

  if(flg_abr)
    AntGamon@abr=R650_avg/R550_avg;



  AntGitelson=( 1.0f/R550 - 1.0f/R700)*R780;
  push(&@ind_lst, "AntGitelson"s);
  AntGitelson@long_name="Anthocyanin (Gitelson)";
  AntGitelson@standard_name="anthocyanin_(gitelson)";
  AntGitelson@description="Anthocyanin (Gitelson) | ( 1.0f/R550 - 1.0f/R700)*R780 | Gitelson et al.(2003,2006)";
  AntGitelson@notes="Anthocyanin (Gitelson) | AntGitelson";
  AntGitelson@units="ratio";
  AntGitelson@label="Reflectance Index";
  AntGitelson@type="trait";
  if(flg_abr)
    AntGitelson@abr=( 1.0f/R550_avg - 1.0f/R700_avg)*R780_avg;


  ChlDela=( R540-R590 ) / ( R540 + R590);
  push(&@ind_lst, "ChlDela"s);
  ChlDela@long_name="Chlorophyll content";
  ChlDela@standard_name="chlorophyll_content";
  ChlDela@description="Chlorophyll content | ( R540-R590 ) / ( R540 + R590) | Delaieux et al. (2014)";
  ChlDela@notes="Chlorophyll content | ChlDela";
  ChlDela@units="ratio";
  ChlDela@label="Reflectance Index";
  ChlDela@type="trait";
  if(flg_abr)
    ChlDela@abr=( R540_avg-R590_avg ) / ( R540_avg + R590_avg);



  NDVI705=( R750-R705 ) / ( R750 + R705);
  push(&@ind_lst, "NDVI705"s);
  NDVI705@long_name="Chlorophyll index";
  NDVI705@standard_name="chlorophyll_index";
  NDVI705@description="Chlorophyll index | ( R750-R705 ) / ( R750 + R705) | Gitelson and Merzlyak (1994)";
  NDVI705@notes="Chlorophyll index | NDVI705";
  NDVI705@units="ratio";
  NDVI705@label="Reflectance Index";
  NDVI705@type="trait";
  if(flg_abr)
    NDVI705@abr=( R750_avg-R705_avg ) / ( R750_avg + R705_avg);



  PRI586=( R531-R586) / ( R531 + R586);
  push(&@ind_lst, "PRI586"s);
  PRI586@long_name="Photochemical reflectance index (586)";
  PRI586@standard_name="photochemical_reflectance_index_(586)";
  PRI586@description="Photochemical reflectance index (586) | ( R531-R586) / ( R531 + R586) | Panigada et al. (2014)";
  PRI586@notes="Photochemical reflectance index (586) | PRI586";
  PRI586@units="ratio";
  PRI586@label="Reflectance Index";
  PRI586@type="trait";
  if(flg_abr)
    PRI586@abr=( R531_avg-R586_avg) / ( R531_avg + R586_avg);


  PRI512=( R531-R512) / ( R531 + R512);
  push(&@ind_lst, "PRI512"s);
  PRI512@long_name="Photochemical reflectance index (512)";
  PRI512@standard_name="photochemical_reflectance_index_(512)";
  PRI512@description="Photochemical reflectance index (512) | ( R531-R512) / ( R531 + R512) | Hernández-Clemente et al. (2011)";
  PRI512@notes="Photochemical reflectance index (512) | PRI512";
  PRI512@units="ratio";
  PRI512@label="Reflectance Index";
  PRI512@type="trait";
  if(flg_abr)
    PRI512@abr=( R531_avg-R512_avg) / ( R531_avg + R512_avg);


  FRI1=R690 / R600;
  push(&@ind_lst, "FRI1"s);
  FRI1@long_name="Fluorescence ratio index1";
  FRI1@standard_name="fluorescence_ratio_index1";
  FRI1@description="Fluorescence ratio index1 | R690 / R600 | Dobrowski et al. (2005)";
  FRI1@notes="Fluorescence ratio index1 | FRI1";
  FRI1@units="ratio";
  FRI1@label="Reflectance Index";
  FRI1@type="trait";
  if(flg_abr)
    FRI1@abr=R690_avg / R600_avg;



  FRI2=R740 / R800;
  push(&@ind_lst, "FRI2"s);
  FRI2@long_name="Fluorescence ratio indices 2";
  FRI2@standard_name="fluorescence_ratio_indices_2";
  FRI2@description="Fluorescence ratio indices 2 | R740 / R800 | Dobrowski et al. (2005)";
  FRI2@notes="Fluorescence ratio indices 2 | FRI2";
  FRI2@units="ratio";
  FRI2@label="Reflectance Index";
  FRI2@type="trait";
  if(flg_abr)
    FRI2@abr=R740_avg / R800_avg;


  NDVI1=(R800-R670)/ (R800+R670);
  push(&@ind_lst, "NDVI1");
  NDVI1@long_name="Normalized Difference Vegetation Index1";
  NDVI1@standard_name="normalized_difference_vegetation_index1";
  NDVI1@description="Normalized Difference Vegetation Index1 | (R800-R670)/ (R800+R670) | Rouse et al. (1973)";
  NDVI1@notes="Normalized Difference Vegetation Index1 | NDVI1";
  NDVI1@units="ratio";
  NDVI1@label="Reflectance Index";
  NDVI1@type="trait";
  if(flg_abr)
    NDVI1@abr=(R800_avg-R670_avg)/ (R800_avg+R670_avg);


  RDVI=NDVI1*0.5f;
  push(&@ind_lst, "RDVI"s);
  RDVI@long_name="Renormalized Difference Vegetation Index";
  RDVI@standard_name="renormalized_difference_vegetation_index";
  RDVI@description="Renormalized Difference Vegetation Index | NDVI1*0.5f | Rougean and Breon (1995)";
  RDVI@notes="Renormalized Difference Vegetation Index | RDVI";
  RDVI@units="ratio";
  RDVI@label="Reflectance Index";
  RDVI@type="trait";
  if(flg_abr)
    RDVI@abr=NDVI1@abr*0.5f;

  SR700_670=R700/R670;
  push(&@ind_lst, "SR700_670"s);
  SR700_670@long_name="Red edge ratio index";
  SR700_670@standard_name="red_edge_ratio_index";
  SR700_670@description="Red edge ratio index | R700/R670 | Part of TCARI index";
  SR700_670@notes="Red edge ratio index | SR700_670";
  SR700_670@units="ratio";
  SR700_670@label="Reflectance Index";
  SR700_670@type="trait";
  if(flg_abr)
    SR700_670@abr=R700_avg/R670_avg;


  SR750_710 = R750 / R710;
  push(&@ind_lst, "SR750_710"s);
  SR750_710@long_name="Red edge";
  SR750_710@standard_name="red_edge";
  SR750_710@description="Red edge | R750 / R710 | Zarco-Tejada et al. (2001)";
  SR750_710@notes="Red edge | SR750_710";
  SR750_710@units="ratio";
  SR750_710@label="Reflectance Index";
  SR750_710@type="trait";
  if(flg_abr)
    SR750_710@abr= R750_avg / R710_avg;


  REP=700.0f +40.0f*(((R670+R780)/2-R700) /(R740-R700));
  push(&@ind_lst, "REP"s);
  REP@long_name="Red edge position";
  REP@standard_name="red_edge_position";
  REP@description="Red edge position | 700.0f +40.0f*(((R670+R780)/2-R700) /(R740-R700)) | Guyot and Baret, 1988";
  REP@notes="Red edge position | REP";
  REP@units="ratio";
  REP@label="Reflectance Index";
  REP@type="trait";
  if(flg_abr)
    REP@abr=700.0f +40.0f*(((R670_avg+R780_avg)/2-R700_avg) /(R740_avg-R700_avg));


  NDRE=(R790-R720)/(R790+R720);
  push(&@ind_lst, "NDRE"s);
  NDRE@long_name="Normalized difference vegetation index";
  NDRE@standard_name="normalized_difference_vegetation_index";
  NDRE@description="Normalized difference vegetation index | (R790-R720)/(R790+R720) | Barnes et al. (2000)";
  NDRE@notes="Normalized differenc vegetation index | NDRE";
  NDRE@units="ratio";
  NDRE@label="Reflectance Index";
  NDRE@type="trait";
  if(flg_abr)
    NDRE@abr=(R790_avg-R720_avg)/(R790_avg+R720_avg);


  TVI=pow( 0.5f, (120.0f*(R750-R550)-200.0f*(R670-R550)));
  push(&@ind_lst, "TVI"s);
  TVI@long_name="Triangular Vegetation Index";
  TVI@standard_name="triangular_vegetation_index";
  TVI@description="Triangular Vegetation Index | 0.5f**(120.0f*(R750-R550)-200.0f*(R670-R550))) | Haboudaneet al. (2004)";
  TVI@notes="Triangular Vegetation Index | TVI";
  TVI@units="ratio";
  TVI@label="Reflectance Index";
  TVI@type="trait";
  if(flg_abr)
    TVI@abr=pow( 0.5f, (120.0f*(R750_avg-R550_avg)-200.0f*(R670_avg-R550_avg)));

 }


// LemnaTec: Broadband Greenness Indices
if(flg_bgi){

  EVI = 2.5f * (R800 - R680) / (R800 + 6.0f * R680 - 7.5f * R450 + 1.0f);
  push(&@ind_lst, "EVI"s);
  EVI@long_name="Enhanced Vegetation Index [Huete et al. (1997)]";
  if(flg_abr)
    EVI@abr= 2.5f * (R800_avg - R680_avg) / (R800_avg + 6.0f * R680_avg - 7.5f * R450_avg + 1.0f);


  ARVI = (R800 - (2.0f * R680 - R450)) / (R800 + (2.0f * R680 - R450));
  push(&@ind_lst, "ARVI"s);
  ARVI@long_name="Atmospherically Resistant Vegetation Index [Kaufman and Tanré (1996)]";
  if(flg_abr)
    ARVI@abr=(R800_avg - (2.0f * R680_avg - R450_avg)) / (R800_avg + (2.0f * R680_avg - R450_avg));

  ETA = (2.0f * ( sqr(R800) - sqr(R680)) + 1.5f * R800 + 0.5f * R680) / (R800 + R680 + 0.5f);
  push(&@ind_lst, "ETA"s);
  if(flg_abr)
    ETA@avg=(2.0f * (sqr(R800_avg) - sqr(R680_avg)) + 1.5f * R800_avg + 0.5f * R680_avg) / (R800_avg + R680_avg + 0.5f);

  GEMI = (ETA * (1.0f - 0.25f * ETA)) - ((R680 - 0.125f) / (1.0f - R680));
  push(&@ind_lst, "GEMI"s);
  GEMI@long_name="Global Environmental Monitoring Index [Pinty and Verstraete (1992)]";
  if(flg_abr)
    GEMI@abr= (ETA@avg * (1.0f - 0.25f * ETA@avg)) - ((R680_avg - 0.125f) / (1.0f - R680_avg));
  
  GARI = (R800 - (R550 - 1.7f * (R450 - R680))) / (R800 + (R550 - 1.7f * (R450 - R680)));
  push(&@ind_lst, "GARI"s);
  GARI@long_name="Green Atmospherically Resistant Index [Gitelson et al. (1996)]";
  if(flg_abr)
     GARI@abr= (R800_avg - (R550_avg - 1.7f * (R450_avg - R680_avg))) / (R800_avg + (R550_avg - 1.7f * (R450_avg - R680_avg)));

  DVI = R800 - R680;
  push(&@ind_lst, "DVI"s);
  DVI@long_name="Difference Vegetation Index [Tucker et al. (1979)]";
  if(flg_abr)
      DVI@abr = R800_avg - R680_avg;

  GDVI = R800 - R550;
  push(&@ind_lst, "GDVI"s);
  GDVI@long_name="Green Difference Vegetation Index [Sripada et al. (2006)]";
  if(flg_abr)
      GDVI@abr = R800_avg - R550_avg;

  
  GNDVI = (R800 - R550) / (R800 + R550);
  push(&@ind_lst, "GNDVI"s);
  GNDVI@long_name="Green Normalized Difference Vegetation Index [Gitelson and Merzlyak (1998)]";
  if(flg_abr)
      GNDVI@abr = (R800_avg - R550_avg) / (R800_avg + R550_avg);

  GRVI = R800 / R550;
  push(&@ind_lst, "GRVI"s);
  GRVI@long_name="Green Ratio Vegetation Index [Sripada et al. (2006)]";
  if(flg_abr)
      GRVI@abr = R800_avg / R550_avg;


  IPVI = R800 / (R800 + R680);
  push(&@ind_lst, "IPVI"s);
  IPVI@long_name="Infrared Percentage Vegetation Index [Crippen et al. (1990)]";
  if(flg_abr)
    IPVI@abr=R800_avg / (R800_avg + R680_avg);

  
  LAI = 3.618f * ((2.5f * (R800 - R680)) / (R800 + 6.0f * R680 - 7.5f * R450 + 1.0f)) - 0.118f;
  push(&@ind_lst, "LAI"s);
  LAI@long_name="Leaf Area Index [Boegh et al. (2002)]";
  if(flg_abr)
    LAI@abr=3.618f * ((2.5f * (R800_avg - R680_avg)) / (R800_avg + 6.0f * R680_avg - 7.5f * R450_avg + 1.0f)) - 0.118f;

  
   
  MSR = ((R800 / R680) - 1.0f) / (sqrt(R800 / R680) + 1);
  push(&@ind_lst, "MSR"s);
  MSR@long_name="Modified Simple Ratio [Chen et al. (1996)]";
  if(flg_abr)
    MSR@abr=((R800_avg / R680_avg) - 1.0f) / (sqrt(R800_avg / R680_avg) + 1);

  NLI = (sqr(R800) - R680) / (sqr(R800) + R680);
  push(&@ind_lst, "NLI"s);
  NLI@long_name="Non-Linear Index [Goel and Qin (1994)]";
  if(flg_abr)
    NLI@abr=(sqr(R800_avg) - R680_avg) / (sqr(R800_avg) + R680_avg);

  
  MNLI = ((pow(R800, 2) - R680) * 1.5f) / (pow(R800, 2) + R680 + 0.5f);
  push(&@ind_lst, "MNLI"s);
  MNLI@long_name="Modified Non-Linear Index [Yang et al. (2008)]";
  if(flg_abr)
    MNLI@abr=((sqr(R800_avg) - R680_avg) * 1.5f) / (sqr(R800_avg) + R680_avg + 0.5f);

  
  SAVI = (1.5f * (R800 - R680)) / (R800 + R680 + 0.5f);
  push(&@ind_lst, "SAVI"s);
  SAVI@long_name="Soil Adjusted Vegetation Index [Huete et al. (1988)]";
  if(flg_abr)
     SAVI@abr = (1.5f * (R800_avg - R680_avg)) / (R800_avg + R680_avg + 0.5f);  

  
  TDVI = sqrt(0.5f + ((R800 - R680) / (R800 + R680)));
  push(&@ind_lst, "TDVI"s);
  TDVI@long_name="Transformed Difference Vegetation Index [Bannari et al. (2002)]";
  if(flg_abr)
      TDVI@abr = sqrt(0.5f + ((R800_avg - R680_avg) / (R800_avg + R680_avg)));

  
  VARI = (R550 - R680) / (R550 + R680 - R450);
  push(&@ind_lst, "VARI"s);
  VARI@long_name="Visible Atmospherically Resistant Index [Gitelson et al. (2002)]";
  if(flg_abr)
      VARI@abr = (R550_avg - R680_avg) / (R550_avg + R680_avg - R450_avg);

 }



  // LemnaTec: Narrowband Greenness Indices
if(flg_ngi){


  RENDVI= (R750 - R705) / (R750 + R705);
  push(&@ind_lst, "RENDVI"s);
  RENDVI@long_name="Red Edge Normalized Difference Vegetation Index [Gitelson and Merzlyak (1994)]";
  if(flg_abr)
      RENDVI@abr = (R750_avg - R705_avg) / (R750_avg + R705_avg);

  mRESR = (R750 - R445) / (R750 + R445);
  push(&@ind_lst, "mRESR"s);
  mRESR@long_name="Modified Red Edge Simple Ratio Index [Sims and Gamon (2002)]";
  if(flg_abr)
     mRESR@abr= (R750_avg - R445_avg) / (R750_avg + R445_avg);

  mRENDVI= (R750 - R705) / (R750 + R705 - 2.0f * R445);
  push(&@ind_lst, "mRENDVI"s);
  mRENDVI@long_name="Modified Red Edge Normalized Difference Vegetation Index [Sims and Gamon (2002)]";
  if(flg_abr)
     mRENDVI@abr=(R750_avg - R705_avg) / (R750_avg + R705_avg - 2.0f * R445_avg);


  VOG1 = R740 / R720;
  push(&@ind_lst, "VOG1"s);
  VOG1@long_name="Vogelmann Red Edge Index 1 [Vogelmann et al. (1993)]";
  if(flg_abr)
      R740_avg / R720_avg;

  VOG2_= (R734 - R747) / (R715 + R726);
  push(&@ind_lst, "VOG2"s);
  VOG2@long_name="Vogelmann Red Edge Index 2 [Vogelmann et al. (1993)]";
  if(flg_abr)
    VOG2@abr= (R734_avg - R747_avg) / (R715_avg + R726_avg);

  VOG3 = (R734 - R747) / (R715 + R720);
  push(&@ind_lst, "VOG3"s);
  VOG3@long_name="Vogelmann Red Edge Index 3 [Vogelmann et al. (1993)]";
  if(flg_abr)
    VOG3@abr=(R734_avg - R747_avg) / (R715_avg + R720_avg);
  
  MCARI = ((R700 - R670) - 0.2f * (R700 - R550)) * (R700 / R670);
  push(&@ind_lst, "MCARI"s);
  MCARI@long_name="Modified Chlorophyll Absorption Reflectance Index [Daughtry et al. (2000)]";
  if(flg_abr)
    MCARI@abr=((R700_avg - R670_avg) - 0.2f * (R700_avg - R550_avg)) * (R700_avg / R670_avg);

  MCARI1 = 1.2f * (2.5f * (R790 - R670) - 1.3f * (R790 - R550));
  push(&@ind_lst, "MCARI1"s);
  MCARI1@long_name="Modified Chlorophyll Absorption Reflectance Index Improved 1 [Haboudane et al. (2004)]";
  if(flg_abr)
     MCARI1@abr=1.2f * (2.5f * (R790_avg - R670_avg) - 1.3f * (R790_avg - R550_avg));

   
  MCARI2  = (1.5f * (2.5f * (R800 - R670) - 1.3f * (R800 - R550))) / sqrt( sqr(2.0f * R800 + 1.0f) - 6.0f * R800 - 5.0f * sqrt(R670) - 0.5f);
  push(&@ind_lst, "MCARI2"s);
  MCARI2@long_name="Modified Chlorophyll Absorption Reflectance Index Improved 2 [Haboudane et al. (2004)]";
  if(flg_abr)
    MCARI2@abr=(1.5f * (2.5f * (R800_avg - R670_avg) - 1.3f * (R800_avg - R550_avg))) / sqrt( sqr(2.0f * R800_avg + 1.0f) - 6.0f * R800_avg - 5.0f * sqrt(R670_avg) - 0.5f);

  
  MTVI  = 1.2f * (1.2f * (R800 - R550) - 2.5f * (R670 - R550));
  push(&@ind_lst, "MTVI"s);
  MTVI@long_name="Modified Triangular Vegetation Index [Haboudane et al. (2004)]";
  if(flg_abr)
      MTVI@abr = 1.2f * (1.2f * (R800_avg - R550_avg) - 2.5f * (R670_avg - R550_avg));

  MTVI2 = 1.5f * (1.2f * (R800 - R550) - 2.5f * (R670 - R550)) / sqrt( sqr(2.0f * R800 + 1.0f) - (6.0f * R800 - 5.0f * sqrt(R670)) - 0.5f);
  push(&@ind_lst, "MTVI2"s);
  MTVI2@long_name="Modified Triangular Vegetation Index Improved [Haboudane et al. (2004)]";
  if(flg_abr)
    MTVI2@abr = 1.5f * (1.2f * (R800_avg - R550_avg) - 2.5f * (R670_avg - R550_avg)) / sqrt(  sqr(2.0f * R800_avg + 1.0f) - (6.0f * R800_avg - 5.0f * sqrt(R670_avg)) - 0.5f);

  
  GMI1 = R750 / R550;
  push(&@ind_lst, "GMI1"s);
  GMI1@long_name="Gitelson and Merzlak Index 1 [Gitelson and Merzlak (1997)]";
  if(flg_abr)
      GMI1@abr = R750_avg / R550_avg;


  GMI2 = R750 / R700;
  push(&@ind_lst, "GMI2"s);
  GMI2@long_name="Gitelson and Merzlak Index 2 [Gitelson and Merzlak (1997)]";
  if(flg_abr)
     GMI2@abr = R750_avg / R700_avg;

   
  G= R554 / R677;
  push(&@ind_lst, "G"s);
  G@long_name="Greenness Index";
  if(flg_abr)
      G@abr = R554_avg / R677_avg;

  
  Lic1 = (R790 - R680) / (R790 + R680);
  push(&@ind_lst, "Lic1"s);
  Lic1@long_name="Lichtenthaler Index 1 [Lichtenthaler et al. 1996]";
  if(flg_abr)
     Lic1@abr = (R790_avg - R680_avg) / (R790_avg + R680_avg);

  
  Lic2 = R440 / R690;
  push(&@ind_lst, "Lic2"s);
  Lic2@long_name="Lichtenthaler Index 2 [Lichtenthaler et al. 1996]";
  if(flg_abr)
     Lic2@abr = R440_avg / R690_avg;


  Lic3 = R440 / R740;
  push(&@ind_lst, "Lic3"s);
  Lic3@long_name="Lichtenthaler Index 3 [Lichtenthaler et al. 1996]";
  if(flg_abr)
      Lic3@abr = R440_avg / R740_avg;

  // LemnaTec: Light Use Efficiency Indices
  PRI531 = (R531 - R570) / (R531 + R570);
  push(&@ind_lst, "PRI531"s);
  PRI531@long_name="Photochemical Reflectance Index [Gamon et al. (1992)]";
  if(flg_abr)
      PRI531@abr = (R531_avg - R570_avg) / (R531_avg + R570_avg);
  

 }

// LemnaTec: Canopy Nitrogen Indices
if(flg_cni){

  
  NDNI = (log(1.0f / R1510) - log(1.0f / R1680)) / (log(1.0f / R1510) + log(1.0f / R1680));
  push(&@ind_lst, "NDNI"s);
  NDNI@long_name="Normalized Difference Nitrogen Index [Fourty et al. (1996)]";
  if(flg_abr)
     NDNI@abr = (log(1.0f / R1510_avg) - log(1.0f / R1680_avg)) / (log(1.0f / R1510_avg) + log(1.0f / R1680_avg));

  
  NRI1510  = (R1510 - R660) / (R1510 + R660);
  push(&@ind_lst, "NRI1510"s);
  NRI1510@long_name="Nitrogen Related Index NRI1510 [Herrmann et al. (2009)]";
  if(flg_abr)
      NRI1510@abr = (R1510_avg - R660_avg) / (R1510_avg + R660_avg);

  
  NRI850 = (R850 - R660) / (R850 + R660);
  push(&@ind_lst, "NRI850"s);
  NRI850@long_name="Nitrogen Related Index NRI850 [Behrens et al. (2006)]";
  if(flg_abr)
    NRI850@abr = (R850_avg - R660_avg) / (R850_avg + R660_avg);
 }



// LemnaTec: Dry or Senescent Carbon Indices
if(flg_sci){

  
  NDLI_= (log(1.0f / R1754) - log(1.0f / R1680)) / (log(1.0f / R1754) + log(1.0f / R1680));
  push(&@ind_lst, "NDLI"s);
  NDLI@long_name="Normalized Difference Lignin Index [Melillo et al. (1982)]";
  if(flg_abr)
     NDLI@abr = (log(1.0f / R1754_avg) - log(1.0f / R1680_avg)) / (log(1.0f / R1754_avg) + log(1.0f / R1680_avg));

  
  CAI = (0.5 * (R2000 - R2200)) / R2100;

  CAI@long_name="Cellulose Absorption Index [Daughtry et al. (2001)]";
  push(&@ind_lst, "CAI"s);
  if(flg_abr)
    CAI@abr = (0.5 * (R2000_avg - R2200_avg)) / R2100_avg;


  PSRI_= (R680 - R500) / R750;
  push(&@ind_lst, "PSRI"s);
  PSRI@long_name="Plant Senescence Reflectance Index [Merzlyak et al. (1999)]";
  if(flg_abr)
     PSRI@abr = (R680_avg - R500_avg) / R750_avg;


 }


// LemnaTec: Leaf Pigments Indices
if(flg_lpi){
  CRI1  = 1.0f / R510 - 1.0f / R550;
  push(&@ind_lst, "CRI1"s);
  CRI1@long_name="Carotenoid Reflectance Index 1 [Gitelson et al. (2002)]";
  if(flg_abr)
    CRI@abr= 1.0f / R510_avg - 1.0f / R550_avg;

   
  CRI2 = 1.0f / R510 - 1.0f / R700;
  push(&@ind_lst, "CRI2"s);
  CRI2@long_name="Carotenoid Reflectance Index 2 [Gitelson et al. (2002)]";
  if(flg_abr)
    CRI2@abr = 1.0f / R510_avg - 1.0f / R700_avg;

  
  ARI1 = 1.0f / R550 - 1.0f / R700;
  push(&@ind_lst, "ARI1"s);
  ARI1@long_name="Anthocyanin Reflectance Index 1 [Gitelson et al. (2001)]";
  if(flg_abr)
    ARI1@abr = 1.0f / R550_avg - 1.0f / R700_avg;

   
  ARI2 = R800 * ((1.0f / R550) - (1.0f / R700));
  push(&@ind_lst, "ARI2"s);
  ARI2@long_name="Anthocyanin Reflectance Index 2 [Gitelson et al. (2001)]";
  if(flg_abr)
    ARI2@abr = R800_avg * ((1.0f / R550_avg) - (1.0f / R700_avg));

  SRPI = R430 / R680;
  push(&@ind_lst, "SRPI"s);
  SRPI@long_name="Simple Ration Pigment Index [Penuelas et al. (1995)]";
  if(flg_abr)
    SRPI@abr = R430_avg / R680_avg;

  
  NPQI = (R415 - R435) / (R415 + R435);
  push(&@ind_lst, "NPQI"s);
  NPQI@long_name="Normalized Phaeophytinization Index [Barnes et al. (1992)]";
  if(flg_abr)
    NPQI@abr = (R415_avg - R435_avg) / (R415_avg + R435_avg);

  NPCI = (R680 - R430) / (R680 + R430);
  push(&@ind_lst, "SRPI"s);
  NPCI@long_name="Normalized Pigment Chlorophyll Index [Penuelas et al. (1994)]";
  if(flg_abr)
      NPCI@abr = (R680_avg - R430_avg) / (R680_avg + R430_avg);
 }


// LemnaTec: Canopy Water Content Indices
if(flg_cwci){
  
  WBI = R900 / R970;
  push(&@ind_lst, "WBI"s);
  WBI@long_name="Water Band Index [Penuelas et al. (1995)]";
  if(flg_abr)
    WBI@abr = R900_avg / R970_avg;

  
  NDWI = (R857 - R1241) / ( R700 + R1241);
  push(&@ind_lst, "NDWI"s);
  NDWI@long_name="Normalized Difference Water Index [Gao et al. (1995)]";
  if(flg_abr)
    NDWI@abr = (R857_avg - R1241_avg) / ( R700_avg + R1241_avg);

 
  MSI = R819 / R1599;
  push(&@ind_lst, "MSI"s);
  MSI@long_name="Moisture Stress Index [Hunt and Rock (1989)]";
  if(flg_abr)
    MSI@abr = R819_avg / R1599_avg;

  
  NDII = (R857 - R1241) / ( R700 + R1241);
  push(&@ind_lst, "NDII"s);
  NDII@long_name="Normalized Difference Infrared Index [Hardisky et al. (1983)]";
  if(flg_abr)
    NDII@abr = (R857_avg - R1241_avg) / ( R700_avg + R1241_avg);
 
  
  NMDI = (R819 - R1649) / (R819 + R1649);
  push(&@ind_lst, "NMDI"s);
  NMDI=NMDI_pxl.avg();
  NMDI@long_name="Normalized Multiband Drought Index [Wang and Qu (2007)]";
  if(flg_abr)
    NMDI@abr = (R819_avg - R1649_avg) / (R819_avg + R1649_avg);

 }

// LemnaTec: Spectral Disease Indices
if(flg_sdi){
  HI = ((R534 - R698) / (R534 + R698)) - (R704 / 2.0f);
  push(&@ind_lst, "HI"s);
  HI@long_name="Healthy Index [Mahlein et al. (2013)]";
  if(flg_abr)
    HI@abr = ((R534_avg - R698_avg) / (R534_avg + R698_avg)) - (R704_avg / 2.0f);


  CLSI = ((R698 - R570) / (R698 + R570)) - R734;
  push(&@ind_lst, "CLSI"s);
  CLSI@long_name="Cercospora Leaf Spot Index [Mahlein et al. (2013)]";
  if(flg_abr)
    CLSI@abr = ((R698_avg - R570_avg) / (R698_avg + R570_avg)) - R734_avg;

  
  SBRI= ((R570 - R513) / (R570 + R513)) + (R704 / 2.0f);
  push(&@ind_lst, "SBRI"s);
  SBRI@long_name="Sugar Beet Rust Index [Mahlein et al. (2013)]";
  if(flg_abr)
    SBRI@abr = ((R570_avg - R513_avg) / (R570_avg + R513_avg)) + (R704_avg / 2.0f);

  PMI = ((R520 - R584) / (R520 + R584)) + R724;
  push(&@ind_lst, "PMI"s);
  PMI@long_name="Powdery Mildew Index [Mahlein et al. (2013)]";
  if(flg_abr)
    PMI@abr = ((R520_avg - R584_avg) / (R520_avg + R584_avg)) + R724_avg;

}

// LemnaTec: Disease Water Stress Indices
if(flg_dwsi){
  
  DWSI1 = R800 / R1660;
  push(&@ind_lst, "DWSI1"s);
  DWSI1@long_name="Disease Water Stress Index 1 [Apan, Held, Phinn and Markley (2003)]";
  if(flg_abr)
    DWSI1@abr = R800_avg / R1660_avg;

  DWSI2 = R1660 / R550;
  push(&@ind_lst, "DWSI2"s);
  DWSI2@long_name="Disease Water Stress Index 2 [Apan, Held, Phinn and Markley (2003)]";
  if(flg_abr)
    DWSI2@abr = R1660_avg / R550_avg;
 
  DWSI3 = R1660 / R680;
  push(&@ind_lst, "DWSI3"s);
  DWSI3@long_name="Disease Water Stress Index 3 [Apan, Held, Phinn and Markley (2003)]";
  if(flg_abr)
    DWSI3@abr = R1660_avg / R680_avg;

  
  DWSI4 = R550 / R680;
  push(&@ind_lst, "DWSI4"s);
  DWSI4@long_name="Disease Water Stress Index 4 [Apan, Held, Phinn and Markley (2003)]";
  if(flg_abr)
     DWSI4@abr = R550_avg / R680_avg;

  DWSI5 = (R800 + R550) / (R1660 + R680);
  push(&@ind_lst, "DWSI5"s);
  DWSI5@long_name="Disease Water Stress Index 5 [Apan, Held, Phinn and Markley (2003)]";
  DWSI5@abr = (R800_avg + R550_avg) / (R1660_avg + R680_avg);
 }

// LemnaTec: Misc Indices + Stress Detection Indices
if(flg_mi){
  
   
  Crt1 = R695 / R420;
  push(&@ind_lst, "Crt1"s);
  CRT1@long_name="Carter Index 1 [Carter (1994)]";
  if(flg_abr)
     Crt1@avg = R695_avg / R420_avg;

  
  Crt2_pxl = R695 / R760;
  push(&@ind_lst, "Crt2"s);
  Crt2@long_name="Carter Index 2 [Carter (1996)]";
  if(flg_abr)
    Crt2@abr = R695_avg / R760_avg;


  BIG2 = R450 / R550;
  push(&@ind_lst, "BIG2"s);
  BIG2@long_name="Blue/Green Index [Zarco-Tejada et al. (2005)]";
  if(flg_abr)
    BIG2@abr = R450_avg / R550_avg;
  
  /*  
  LSI_pxl = R1110 / R810;
  LSI=LSI_pxl.avg();
  LSI@long_name="Leaf Structure Index [Maruthi Sridhar et al. (2007)]";
  if(flg_abr)
       LSI@abr = R1110_avg / R810_avg; 
  */
 
  BRI = ((1.0f / R550) - (1.0f / R700)) / R800;
  push(&@ind_lst, "BRI"s);
  BRI@long_name="Browning Reflectance Index [Chivkunova et al. (2001)]";
  if(flg_abr)
     BRI@abr = ((1.0f / R550_avg) - (1.0f / R700_avg)) / R800_avg;

}










/* create avg from pixel formula 
 this creates the vars NDVI, SR, OSAVI by taking average of the vars NDVI_pxl, SR_pxl, OSAVI_pxl  
 and also min,max that is NDVI@min, NDVI@max, SR@min, SR@max, OSAVI@min, OSAVI@max ...,*/

if(flg_agg){ 
  /* nb sz and idx are already RAM vars */

  
  if(flg_dbg)
    print(@ind_lst);
  

  sz=@ind_lst.size();

 
  for(idx=0;idx<sz;idx++)   
  {
     @name=sprint(@ind_lst(idx));
     @name_min=push(@name,"@min");
     @name_max=push(@name,"@max");
     @name_avg=push(@name,"@avg");

     if(exists(*@name))
     {
       // average of ratio 
       // *@name=*@name_pxl.avg();
       // min
       *@name_min=*@name.min();    
       // max
       *@name_max=*@name.max();    
       *@name_avg=*@name.avg();    

     }  
  }  
}  

#include "hyperspectral_indices_meta.nco"


//do clean up of global -atts - aka vpointers
if(exists(@rfl_lst))   ram_delete(@rfl_lst);
if(exists(@dbl_val_lst)) ram_delete(@dbl_val_lst);
if(exists(@ind_lst)) ram_delete(@ind_lst);
if(exists(@R_nm)) ram_delete(@R_nm);
if(exists(@iR_nm)) ram_delete(@iR_nm);

if(exists(@var_nm)) ram_delete(@var_nm);
if(exists(@var_inm)) ram_delete(@var_inm);
if(exists(@var_nm_avg)) ram_delete(@var_nm_avg);



if(exists(@vars_abr)) ram_delete(@vars_abr);
if(exists(@name)) ram_delete(@name);
if(exists(@name_avg)) ram_delete(@name_avg);
if(exists(@name_min)) ram_delete(@name_min);
if(exists(@name_max)) ram_delete(@name_max);

